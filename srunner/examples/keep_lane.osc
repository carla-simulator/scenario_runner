import basic.osc

scenario top:
    path: Path                      # A path in the map
    path.set_map("Town04")    # specify map to use in this test
    path.path_min_driving_lanes(2)         # Path should have at least two lanes

    ego_vehicle: Model3                # ego car
    npc: Rubicon               # The other car

    event start
    event end
    do serial:
        get_ahead: parallel(duration: 15s):
            ego_vehicle.drive(path) with:
                speed(30kph)
                lane(1, at: start) # left to right: [1..n]
            npc.drive(path) with:
                lane(right_of: ego_vehicle, at: start)
                position(15m, behind: ego_vehicle, at: start)
                position(20m, ahead_of: ego_vehicle, at: end)       

        slow: parallel(duration: 10s):
            ego_vehicle.drive(path)
            npc.drive(path) with:
                speed(30kph)

        keep_lane: parallel(duration: 13s):
            ego_vehicle.drive(path) with:
                keep_lane()
            npc.drive(path) with:
                keep_lane()

